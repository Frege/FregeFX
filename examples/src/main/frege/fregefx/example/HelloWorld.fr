{--
	A simple example of a JavaFX UI with Frege:
	a group with a vbox of two buttons plus simple onAction handlers.
	UI construction follows a _builder_ style approach,
	i.e. the implementation details of how to add sub nodes to a parent is hidden
	but the containment structure is visible in the code layout.
-}
module fregefx.example.HelloWorld where

import frege.Prelude hiding(ListView)
import fregefx.JavaFxType
import fregefx.JavaFxAll
import fregefx.JavaFxUtils

main args = 
    FregeFX.launch $ withStage buildUI

buildUI :: Group -> Stage -> JFX Group
buildUI root stage = do
    stage.setTitle "FregeFX Hello World"
    root <: do
        vbox <- VBox.new 5d :: JFX VBox
        vbox.setPadding =<< insets 10
        vbox <: do
            button <- Button.new "Please click me"
            button `action_` (button.setText "Thanks!")
        vbox <: do
            button <- Button.new "do side effect"
            -- println "side effect was here" -- not allowed here so it does not compile!
            button `action_` (println "I am IO!") -- side effect my occur where allowed
        -- experiment that dynamically adds 5 buttons
        vbox `addAll` map (Button.new . ("Button "++) . show) [1..5]


